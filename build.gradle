plugins {
	id "org.springframework.boot" version "latest.release"
	id "io.spring.dependency-management" version "latest.release"
	id "io.freefair.lombok" version "latest.release"
	id "java"
	id "war"
	id "maven-publish"
}

group = "com.example"
version = "0.0.4-SNAPSHOT"
sourceCompatibility = "1.8"

repositories {
	mavenCentral()
	maven { 
		url "${repoUrlPrefix}public/" 
		allowInsecureProtocol true
	}
}

configurations {
    all {
        resolutionStrategy.cacheChangingModulesFor 5, 'minutes'
        resolutionStrategy.cacheDynamicVersionsFor 24, 'hours'
    }
}

dependencies {
	ext {
		swaggerVersion = "3.0.0"
	}
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-cache"
	implementation "org.springframework.boot:spring-boot-starter-data-redis"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.kafka:spring-kafka"
    implementation "org.springframework.session:spring-session-data-redis"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:latest.release"
    implementation "io.opentracing.contrib:opentracing-jdbc:latest.release"
    implementation "io.springfox:springfox-boot-starter:$swaggerVersion"
	implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
	modules {
		module("org.springframework.boot:spring-boot-starter-logging") {
			replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
		}
	}
    developmentOnly "org.springframework.boot:spring-boot-devtools"
	runtimeOnly "mysql:mysql-connector-java"
	
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.security:spring-security-test"
	testRuntimeOnly "com.h2database:h2"
}

test {
	useJUnitPlatform()
}

java {
	withSourcesJar()
}

processResources {
    filesMatching('application.*') {
        expand(project.properties)
    }
}

configurations {
	[apiElements, runtimeElements].each {
		it.outgoing.artifacts.removeIf {
			it.buildDependencies.getDependencies(null).contains(jar)
		}
		it.outgoing.artifact(bootJar)
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			versionMapping {
				usage("java-api") {
					fromResolutionOf("runtimeClasspath")
				}
				usage("java-runtime") {
					fromResolutionResult()
				}
			}
		}
	}
	repositories {
		maven {
			if(version.endsWith("-SNAPSHOT")) {
				url "${repoUrlPrefix}snapshots/"
			} else {
				url "${repoUrlPrefix}releases/"
			}
			allowInsecureProtocol true
			credentials {
				username repoUser
				password repoPassword
			}
		}
	}
	
}
